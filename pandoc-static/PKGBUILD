# Maintainer: Ruud van Asseldonk <dev@veniogames.com>
pkgname=pandoc-static
pkgver=1.19.2.1
pkgrel=1
pkgdesc="Universal markup converter"
arch=('i686' 'x86_64')
url='https://pandoc.org/'
license=('GPL2')
depends=('gmp' 'libffi' 'zlib')
optdepends=('texlive-core: for conversion to pdf')
makedepends=('git' 'stack')
provides=("${pkgname%-static}")
conflicts=("${pkgname%-static}")
source=("git+https://github.com/jgm/pandoc.git#tag=${pkgver}")
md5sums=('SKIP')

build() {
  cd "$srcdir/${pkgname%-static}"
  stack setup
  stack build
}

check() {
  cd "$srcdir/${pkgname%-static}"
  stack test
}

package() {
  cd "$srcdir/${pkgname%-static}"
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/licenses/${pkgname%-static}"
  cp "$(stack path --local-install-root)/bin/pandoc" "$pkgdir/usr/bin/"
  cp "COPYING.md" "$pkgdir/usr/share/licenses/${pkgname%-static}/"
}
